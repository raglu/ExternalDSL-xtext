grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

GameWorld:
	'GameWorld' name=STRING elements+=Element*;

Element:
	 GameRoom | GameEntity | GameRules;
	 
GameRoom:
	'room' name=ID entityName=STRING roomAttributes+=RoomAttribute*;

RoomAttribute:
	PathList | PlayerList | ItemList | NPCList | WeaponList | HostileNPCList;

PathList:
	'paths' ':' paths+=Path (','? paths+=Path)*;

Path:
	toType=[GameRoom]
	'through' entityName=STRING
	('requires' requireItem=[Item])?;

PlayerList:
	'players' ':' playerList+=[Player] (','? playerList+=[Player])*;

ItemList:
	'items' ':' itemList+=[Item] (','? itemList+=[Item])*;

WeaponList:
	'weapons' ':' weaponsList+=[Weapon] (','? weaponsList+=[Weapon])*;

NPCList:
	'npcs' ':' npcList+=[NPC] (','? npcList+=[NPC])*;

HostileNPCList:
	'hostileNpcs' ':' hostileNPCList+=[HostileNPC] (','? hostileNPCList+=[HostileNPC])*;

GameEntity:
	Player | Item | Weapon | NPC | HostileNPC;

Player:
	'player' name=ID entityName=STRING (':' playerType=[Player])?
	attributes+=EntityAttribute*;

Item:
	'item' name=ID entityName=STRING (':' itemType=[Item])?
	attributes+=EntityAttribute*;

Weapon:
	'weapon' name=ID entityName=STRING (':' weaponType=[Weapon])?
	attributes+=EntityAttribute*;

NPC:
	'npc' name=ID entityName=STRING (':' npcType=[NPC])?
	attributes+=EntityAttribute*;

HostileNPC:
	'hostilenpc' name=ID entityName=STRING (':' hostileNpcType=[HostileNPC])?
	attributes+=EntityAttribute*;

EntityAttribute:
	Health | Weight | Damage | Durability | CarryCapacity | Escapeable | Aggressive | Attribute;

Health:
	'health' '=' health=INT;

Weight:
	'weight' '=' weight=INT;

Damage:
	'damage' '=' damage=INT;

Durability:
	'durability' '=' durability=INT;

CarryCapacity:
	'carryCapacity' '=' carryCapacity=INT;

Escapeable:
	'escapeable' '=' (escapeable?='true' | escapeable?='false');

Aggressive:
	'aggressive' '=' (aggressive?='true' | aggressive?='false');

Attribute:
	name=ID '=' (stringValue=STRING | intValue=INT);

GameRules:
	{GameRules} 'GameRules:' rules+=Rule*;

Rule:
	ConditionalRule | TimeRule
;

TimeRule:
	'time' '=' time=INT;

ConditionalRule:
	'if' condition+=Condition 'then' (action=STRING | assignment=STRING '=' (newInt=INT | newString=STRING));

Condition:
	left=Var op=CompareOp right=Var;

CompareOp:
	'<' | '>' | '==' | '<=' | '>=';

Var:
	type=[GameEntity] '.' attribute=ID | value=INT
;
